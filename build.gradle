buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:3.1.0'
    }
}
plugins {
    id "com.github.hierynomus.license" version "0.12.1"
    id "com.jfrog.bintray" version "1.6"
    id 'net.researchgate.release' version '2.3.4'
}

apply plugin: 'java'
apply plugin: 'idea'

apply plugin: 'osgi'
apply plugin: 'biz.aQute.bnd.builder'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

idea {
    project {
        languageLevel = '1.8'
        vcs = 'Git'
    }
}

release {
    tagTemplate = 'v${version}'
    failOnPublishNeeded = false
}

jar {
    manifest {
        instruction 'Import-Package', 'javax.validation.constraints', '*'
    }
}

repositories {
    mavenCentral()
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-parameters"
    }
}

dependencies {
    compile 'javax.validation:validation-api:1.1.0.Final'
    compile 'com.graphql-java:graphql-java:2.2.0'

    // OSGi
    compileOnly 'org.osgi:org.osgi.core:6.0.0'
    compileOnly 'org.osgi:org.osgi.service.cm:1.5.0'
    compileOnly 'org.osgi:org.osgi.service.component:1.3.0'
    compileOnly 'biz.aQute.bnd:biz.aQute.bndlib:3.2.0'

    testCompile 'org.testng:testng:6.9.10'
}

test.useTestNG()

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId 'com.graphql-java'
            artifactId project.name
            version project.version

            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'graphql-java-annotations'
                    description 'Annotations-based syntax for GraphQL schema definition'
                    url 'https://github.com/graphql-java/graphql-java-annotations'
                    inceptionYear '2016'

                    scm {
                        url 'https://github.com/graphql-java/graphql-java-annotations'
                        connection 'scm:https://yrashk@github.com/graphql-java/graphql-java-annotations.git'
                        developerConnection 'scm:git://github.com/graphql-java/graphql-java-annotations.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'yrashk'
                            name 'Yurii Rashkovskii'
                            email 'yrashk@gmail.com'
                        }
                        developer {
                            id 'apottere'
                            name 'Andrew Potter'
                            email 'Andrew.Potter@coxautoinc.com'
                        }
                    }
                }
            }
        }
    }
}

afterReleaseBuild.dependsOn bintrayUpload

bintray {
    user = System.getenv('BINTRAY_USER') ?: project.findProperty('BINTRAY_USER') ?: ''
    key = System.getenv('BINTRAY_KEY') ?: project.findProperty('BINTRAY_KEY') ?: ''
    publications = ['maven']
    publish = true
    pkg {
        repo = 'maven'
        name = project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/graphql-java/graphql-java-servlet'
        version {
            name = project.version
        }
    }
}
