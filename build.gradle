buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.5.3"
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}
plugins {
    id "com.github.hierynomus.license" version "0.12.1"
    id "com.jfrog.bintray" version "1.6"
    id 'net.researchgate.release' version '2.3.4'
}

apply plugin: 'java'
apply plugin: 'idea'

apply plugin: 'osgi'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'com.bmuschko.nexus'


idea {
    project {
        languageLevel = '1.8'
        vcs = 'Git'
        ipr.withXml { xmlFile ->
            // enable 'Annotation Processors'
            xmlFile.asNode().component.find {
                println(it)
                return it.@name == 'CompilerConfiguration'
            }['annotationProcessing'][0].replaceNode {
                annotationProcessing {
                    profile(default: true, name: 'Default', useClasspath: 'true', enabled: true)
                }
            }
        }
        // TODO: add -parameters to IDEA's javac
    }
}

release {
    tagTemplate = 'v${version}'
    failOnPublishNeeded = false
}

jar {
    manifest {
        instruction 'Import-Package', 'javax.validation.constraints', '*'
    }
}

repositories {
    mavenCentral()
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-parameters"
    }
}

dependencies {
    compile 'javax.validation:validation-api:1.1.0.Final'
    compile 'com.graphql-java:graphql-java:2.1.0'

    // Remove some boilerplate
    compile 'org.projectlombok:lombok:1.16.6'

    testCompile 'org.testng:testng:6.9.10'
}

test.useTestNG()

modifyPom {
     project {
         name 'graphql-java-annotations'
         description 'Annotations-based syntax for GraphQL schema definition'
         url 'https://github.com/graphql-java/graphql-java-annotations'
         inceptionYear '2016'

         scm {
             url 'https://github.com/graphql-java/graphql-java-annotations'
             connection 'scm:https://yrashk@github.com/graphql-java/graphql-java-annotations.git'
             developerConnection 'scm:git://github.com/graphql-java/graphql-java-annotations.git'
         }

         licenses {
             license {
                 name 'The Apache Software License, Version 2.0'
                 url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                 distribution 'repo'
             }
         }

         developers {
             developer {
                 id 'yrashk'
                 name 'Yurii Rashkovskii'
                 email 'yrashk@gmail.com'
             }
         }
     }
 }

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId 'com.graphql-java'
            artifactId project.name
            version project.version

            artifact sourcesJar
            artifact javadocJar
        }
    }
}
